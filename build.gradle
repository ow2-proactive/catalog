buildscript {
    repositories {
        jcenter()
        maven {
            url "http://repository.activeeon.com/content/groups/proactive/"
        }
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    dependencies {
        classpath 'org.springframework.boot:spring-boot-gradle-plugin:1.3.5.RELEASE'
        classpath 'com.diffplug.gradle.spotless:spotless:2.4.0'
        classpath 'org.ow2.proactive:coding-rules:1.0.0'
        classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.5.0'
    }

    dependencies {
        delete "gradle/ext"
        ant.unjar src: configurations.classpath.find { it.name.startsWith("coding-rules") }, dest: 'gradle/ext'
    }
}

plugins {
    id 'antlr'
    id 'eclipse'
    id 'idea'
    id 'jacoco'
    id 'java'
    id 'maven'
    id 'war'
}

apply plugin: 'spring-boot'
apply plugin: 'com.github.kt3k.coveralls'
apply from: "$rootDir/gradle/ext/coding-format.gradle"

group = 'org.ow2.proactive'
version = version

bootRepackage {
    enabled = true
}

configurations {
    // The following module is excluded to avoid clashes when embedded inside the ProActive Scheduler
    all*.exclude module: 'spring-boot-starter-logging'

    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime

    querydsl
}

compileJava {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
}

coveralls {
    sourceDirs = allprojects.sourceSets.main.allSource.srcDirs.flatten()
    jacocoReportPath = "${buildDir}/reports/jacoco/test/jacocoTestReport.xml"
}

ext {
    generatedSrcDir = "$projectDir/src/generated/java"
}

generateGrammarSource {
    arguments += ["-visitor", "-long-messages"]
    maxHeapSize = "64m"
    outputDirectory = file("$generatedSrcDir/org/ow2/proactive/catalog/rest/query/parser")
}

repositories {
    jcenter()
    maven {
        url "http://repository.activeeon.com/content/groups/proactive/"
    }
}

sourceSets {
    generated {
        java {
            srcDirs += generatedSrcDir
        }
    }

    main {
        java {
            srcDirs += generatedSrcDir
        }
    }

    integrationTest {
        java.srcDir file('src/integration-test/java')
        resources.srcDir file('src/integration-test/resources')

        compileClasspath = sourceSets.main.output + configurations.integrationTestCompile
        runtimeClasspath = output + compileClasspath + configurations.integrationTestRuntime
    }
}

springBoot {
    mainClass = 'org.ow2.proactive.catalog.Application'
}

war {
    baseName = "catalog"
    version = version
}

dependencies {
    antlr 'org.antlr:antlr4:4.5.2'

    compile 'commons-fileupload:commons-fileupload:1.3.1'
    compile 'org.apache.commons:commons-lang3:3.5'
    compile 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.7.1'
    compile 'com.mysema.querydsl:querydsl-jpa:3.7.1'
    querydsl 'com.mysema.querydsl:querydsl-apt:3.7.1'

    /*
        Spring boot 1.3.X relies on hibernate 4 which is not compatible
        with the version that is used with Scheduling project
     */
    compile('org.springframework.boot:spring-boot-starter-data-jpa:1.3.5.RELEASE') {
        exclude module: 'hibernate-entitymanager'
    }
    compile 'org.hibernate:hibernate-entitymanager:5.1.0.Final'
    compile 'org.hibernate:hibernate-core:5.1.0.Final'

    compile 'org.springframework.boot:spring-boot-starter-log4j2:1.3.5.RELEASE'
    compile('org.springframework.boot:spring-boot-starter-web:1.3.5.RELEASE') {
        exclude module: 'spring-boot-starter-tomcat'
    }

    compile 'javax.validation:validation-api:1.1.0.Final'

    compile 'org.springframework.hateoas:spring-hateoas:0.19.0.RELEASE'

    compile 'org.eclipse.jetty:jetty-servlets:9.2.14.v20151106'

    providedCompile 'org.springframework.boot:spring-boot-starter-tomcat:1.3.5.RELEASE'

    providedRuntime 'org.hsqldb:hsqldb:r5658-f2e27a7'

    compile 'io.springfox:springfox-spring-web:2.4.0'
    compile 'io.springfox:springfox-swagger2:2.4.0'
    compile 'io.springfox:springfox-swagger-ui:2.4.0'

    compile 'com.graphql-java:graphql-java:3.0.0'
    compile 'org.projectlombok:lombok:1.16.16'

    compile 'org.zeroturnaround:zt-zip:1.11'

    testCompile 'com.google.truth:truth:0.28'
    testCompile 'junit:junit:4.12'
    testCompile 'nl.jqno.equalsverifier:equalsverifier:1.7.7'
    testCompile 'org.mockito:mockito-core:1.10.19'

    integrationTestCompile 'com.google.code.gson:gson:2.5'
    integrationTestCompile 'com.jayway.restassured:rest-assured:2.8.0'
    integrationTestCompile 'org.springframework.boot:spring-boot-starter-test:1.3.5.RELEASE'
}

test {
    jacoco {
        destinationFile = file("$buildDir/jacoco/jacocoTest.exec")
    }
}

task integrationTest(type: Test) {
    testClassesDir = sourceSets.integrationTest.output.classesDir
    classpath = sourceSets.integrationTest.runtimeClasspath
    classpath += sourceSets.test.runtimeClasspath

    jacoco {
        destinationFile = file("$buildDir/jacoco/jacocoIntegrationTest.exec")
    }

    testLogging {
        events "passed", "skipped", "failed"
    }
}

task generateQueryDSL(type: JavaCompile, group: 'build', description: 'Generates the QueryDSL query types') {
    // place the output of the QueryDSL predicate processor in a new location.
    def outputDir = file("$generatedSrcDir")
    outputs.dir outputDir
    doFirst {
        // make sure the directory exists.
        outputDir.exists() || outputDir.mkdirs()
    }

    source = sourceSets.main.java
    classpath = configurations.compile + configurations.querydsl

    options.compilerArgs = [
            "-proc:only",
            "-processor", "com.mysema.query.apt.jpa.JPAAnnotationProcessor"
    ]

    // generated code goes into the generated code directory.
    destinationDir = outputDir

    dependencyCacheDir = compileJava.dependencyCacheDir

}

compileJava.dependsOn generateQueryDSL
compileJava.source generateQueryDSL.outputs.files, sourceSets.main.java

jacocoTestReport {
    executionData test, integrationTest

    sourceSets project.sourceSets.main

    reports {
        html.enabled = true
        xml.enabled = true
    }

    // remove auto-generated classes from report
    afterEvaluate {
        classDirectories = files(classDirectories.files.collect {
            fileTree(dir: it, excludes: [
                    'org/ow2/proactive/catalog/rest/entity/Q*',
                    'org/ow2/proactive/catalog/rest/query/parser/*']);
        })
    }
}

clean {
    sourceSets.generated.java.srcDirs.each {
       srcDir -> delete srcDir
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.10'
}

check.dependsOn integrationTest

jacocoTestReport.dependsOn check
tasks.coveralls.dependsOn jacocoTestReport

tasks.withType(Test) {
    reports.html.destination = file("${reporting.baseDir}/${name}")
}

idea {
    module {
        testSourceDirs += file('src/integration-test/java')
        testSourceDirs += file('src/integration-test/resources')

        scopes.TEST.plus += [
                configurations.integrationTestCompile,
                configurations.integrationTestRuntime
        ]
    }
}
